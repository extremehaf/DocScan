//noinspection GradleDependency
apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'realm-android'

def computeVersionCode() {

    if (System.env.BUILD_NUMBER) {              // Check if there is a system build number.
        return "$System.env.BUILD_NUMBER".toInteger()
    } else if (System.env.CIRCLE_BUILD_NUM) {   // Check to see if this is built on circle CI
        return "$System.env.CIRCLE_BUILD_NUM".toInteger()
    } else {
        return 1                                // if none is found, default to 1.
    }
}

def computeVersionName() {
    return "1." + computeVersionCode()
}


android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "scan.lucas.com.docscan"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode computeVersionCode()
        versionName computeVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -frtti -fexceptions" //EDIT
                //abiFilters 'x86', 'x86_64', 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'mips', 'mips64' //EDIT
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['/src/main/jniLibs']
            //EDIT: Use your custom location to jniLibs. Path given is only for example purposes.
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
        /*ndkBuild {
            path 'src/main/jni/OpenCV.mk'
        }*/
    }
}



dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.23.4'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'


    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

//    implementation 'com.google.code.gson:gson:2.8.2'
//
//    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
//    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
//    implementation 'com.squareup.okhttp3:okhttp:3.10.0'

    implementation 'com.amazonaws:aws-android-sdk-core:2.6.+'
    implementation 'com.amazonaws:aws-android-sdk-ddb:2.6.+'
    implementation('com.amazonaws:aws-android-sdk-mobile-client:2.6.7@aar') { transitive = true }
    implementation 'com.amazonaws:aws-android-sdk-s3:2.6.+'
    implementation 'com.amazonaws:aws-android-sdk-cognito:2.6.+'

    implementation 'com.itextpdf:itextg:5.5.10'
    implementation 'com.github.barteksc:pdfium-android:1.9.0'

    implementation 'io.realm:android-adapters:2.1.1'

    implementation 'com.theartofdev.edmodo:android-image-cropper:2.7.+'

    implementation project(':openCVLibrary331')
}
